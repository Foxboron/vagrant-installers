pkgname=hashicorp-vagrant
pkgver="${VAGRANT_VERSION:-unknown}"
pkgrel=1
pkgdesc="Build and distribute virtualized development environments"
arch=('x86_64')
url="https://www.vagrantup.com"
license=('MIT')
_pkg_ver="${VAGRANT_VERSION:-unknown}"
source=("local://substrate.tar.gz"
        "local://vagrant.gem")
conflicts=('vagrant' 'vagrant-substrate')
options=('!emptydirs')
md5sums=('SKIP' 'SKIP')
depends=('libffi>=3.2.1' 'libxml2>=2.9.4' 'libxslt>=1.1.29'
         'libyaml' 'zlib>=1.2.11' 'xz>=5.2.3' 'readline>=6.3'
         'openssl>=1.0.2l' 'libssh2>=1.8.0' 'libarchive' 'curl>=7.54.0' 'ruby>=2.3.4'
         'rsync' 'autoconf' 'automake' 'binutils' 'bison' 'flex' 'gawk' 'gcc' 'gettext'
         'grep' 'groff' 'gzip' 'libtool' 'm4' 'make' 'patch' 'pkg-config' 'sed' 'util-linux'
         'which')
makedepends=('ruby' 'go')

prepare() {
  mkdir -p "${srcdir}/opt/vagrant/embedded/etc"
  mkdir -p "${srcdir}/opt/vagrant/embedded/rgloader"
  mkdir -p "${srcdir}/opt/vagrant/bin"
}

build() {
  SUBSTRATE_DIR="${srcdir}/substrate"
  VAGRANT_DIR=$srcdir
  INSTALL_DIR="${srcdir}/opt/vagrant"
  EMBEDDED_DIR="${INSTALL_DIR}/embedded"

  # Install the required substrate items
  cp "${SUBSTRATE_DIR}/common/gemrc" "${EMBEDDED_DIR}/etc/gemrc"
  cp "${SUBSTRATE_DIR}/substrate/common/rgloader/"* "${EMBEDDED_DIR}/rgloader/"
  cp "${SUBSTRATE_DIR}/substrate/linux/rgloader/"* "${EMBEDDED_DIR}/rgloader/"

  # Install vagrant launcher
  pushd "${SUBSTRATE_DIR}/launcher"
  go get github.com/mitchellh/osext
  go build -o "${INSTALL_DIR}/bin/vagrant"
  popd

  # Install Vagrant and the share plugin
  pushd "${srcdir}"
  export GEM_PATH="${EMBEDDED_DIR}/gems/${VAGRANT_VERSION}"
  export GEM_HOME="${GEM_PATH}"
  export GEMRC="${EMBEDDED_DIR}/etc/gemrc"

  gem install ./vagrant.gem --no-document
  cat <<EOF >${EMBEDDED_DIR}/plugins.json
{
    "version": "1",
    "installed": {
    }
}
EOF
  chmod 0644 ${EMBEDDED_DIR}/plugins.json
  cat <<EOF >${EMBEDDED_DIR}/manifest.json
{
    "vagrant_version": "${VAGRANT_VERSION}"
}
EOF
  chmod 0644 ${EMBEDDED_DIR}/manifest.json
  popd

}

package() {
  mkdir -p "${pkgdir}/opt"
  mv "${srcdir}/opt/vagrant" "${pkgdir}/opt/"
  mkdir -p "${pkgdir}/usr/bin"
  ln -s /opt/vagrant/bin/vagrant "${pkgdir}/usr/bin/vagrant"
}
